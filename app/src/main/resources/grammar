// Grammar reference for Kix (just for ref, not used to generate anything)

// First, we define some terms:
Literal:           Number, Strings, Booleans and nil
Unary Expression:  Prefix ! to perform logical not, and - to negate a number
Binary Expression: Infix arithmetic (+, -, *, /) and logic (==, !=, <, <=, >, >=)
Parenthesis:       Pair of ( and ) wrapped around an expression

// Now, the grammar production rules

// Program
program         -> declaration* EOF ;

declaration     -> varDecl
                 | statement ;

// Declarations
varDecl         -> "var" IDENTIFIER ( "=" expression )? ;

statement       -> exprStmt
                 | ifStmt
                 | printStmt
                 | blockStmt ;

// Statements
exprStmt        -> expression ;

ifStmt          -> "if" "(" expression ")" statement
                   ("else" statement )? ;

printStmt       -> "print" expression ;

blockStmt       -> "{" declaration* "}" ;


// Expression
expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment
                 | ternary ;

// TODO: think about this
ternary     -> equality (? expression : expression)* ;

// precedence rules
equality        -> comparison ( ( "!=" | "==" ) comparison )* ;

comparison      -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term            -> factor ( ( "-" | "+" ) factor )* ;

factor          -> unary ( ( "/" | "*" ) unary )* ;

unary           -> ( "!" | "-" ) unary
                 | primary ;

primary         -> "true" | "false" | "nil"
                 | NUMBER | STRING
                 | "(" expression ")"
                 | IDENTIFIER ;
